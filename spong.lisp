;; Copyright (C) 2020  Mauro Aranda

;; S-Pong is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.

;; S-Pong is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with S-Pong.  If not, see <http://www.gnu.org/licenses/>.

(in-package #:com.the-blackbeard.spong)

;;; S-Pong misc functions.
(defun set-serving-player ()
  "Set a new serving player, randomly.  Modifies `*serving-player*'.
This function is used when the serving player should be random: after the user
press RET."
  (setf *serving-player* (if (eql (random 2) 0) *player-left* *player-right*)))

;;; A game class.
(defclass game ()
  ((name :initarg :game-name :accessor game-name)
   (state :initform 'start :accessor game-state)))

(defun init-audio (&key (freq 22050) (chann 1) (cs 1024) (sfx nil))
  "Helper S-Pong function to initialise the audio system.
Initialise all sounds and modify `*sounds*' accordingly."
  (sdl-mixer:init-mixer)
  (if (sdl-mixer:open-audio :frequency freq :channels chann :chunksize cs)
      (setf *sounds* (when sfx
		       (mapcan #'(lambda (a)
				   (list (fx-name a)
					 (sdl-mixer:load-sample
					  (sdl:create-path (resource a)
							   *asset-dir*))))
			       sfx)))
      (error "Could not load mixer.")))

(defmethod init ((game game))
  (sdl:with-init (sdl:sdl-init-video sdl:sdl-init-audio)
    (init-audio :sfx (mapcar #'(lambda (fx) (make-instance 'audio-fx
							   :fx-name (car fx)
							   :resource (cadr fx)))
			     '((:paddle-hit "paddle_hit.wav")
			       (:wall-hit "wall_hit.wav")
			       (:score "score.wav")
			       (:powerup "powerup.wav"))))
    (game-load game)
    (game-draw game)
    (sdl:update-display)
    (sdl:with-events (:poll)
      (:quit-event () t)
      (:key-down-event (:key key) (process-key game key))
      (:video-expose-event () (sdl:update-display))
      (:idle () (process-event game)))))

(defmethod game-load ((game game))
  (setf *random-state* (make-random-state t))
  (sdl:window *window-width* *window-height* :title-caption (game-name game))
  (setf (sdl:frame-rate) *frame-rate*)
  (sdl:initialise-default-font *ttf-spong-font*)
  (setf sdl:*default-color* sdl:*white*)
  (setf *player-left* (make-instance 'left-player :position :left :serve-op '+))
  (setf *player-right* (make-instance 'right-player-ai :position :right
				      :serve-op '-))
  (setf *ball* (make-instance 'spong-ball :side *ball-length*))
  (setf *game-objects* (list *ball* *player-left* *player-right*))
  (setf *players* (remove *ball* *game-objects*))
  (setf *current-modifier* nil)
  (setf *modifiers*
	(pairlis
	 '(defensive-powerup quick-powerup power-powerup)
	 (mapcar #'(lambda (f) (make-instance 'sprite :file f))
		 '("defensive.bmp" "quick.bmp" "power.bmp"))))
  (setf *mod-rate-appearence* (random-ab 2 10)))

(defmethod process-key ((game game) key)
  (cond
    ((sdl:key-pressed-p :SDL-KEY-ESCAPE) (sdl:push-quit-event))
    ((sdl:key-pressed-p :SDL-KEY-RETURN)
     (cond
       ((eq (game-state game) 'start)
	(setf (game-state game) 'serve)
	(set-serving-player))
       ((eq (game-state game) 'done)
	(setf (game-state game) 'serve)
	(mapcar #'reset *game-objects*))
       (t
	(setf (game-state game) 'serve)
	(setf *current-modifier* nil)
	(mapcar #'(lambda (p) (when (modifier p) (expire (modifier p) p)))
		*players*)
	(set-serving-player)
	(reset *ball*))))))

(defun draw-score ()
  "Draw score to the screen, with font `*ttf-score-font*'."
  (sdl:with-font (f *ttf-score-font*)
    (let ((str (format nil "狺磲疸狎＇沲蝌孱舡筱矧痨狴弪螵┅┅箐旌潋狩篝蜷铉箫扉洵篝ōǒ鏖钿秣鏖漪瑾博蛋ǒ鏖钿秣桢殓梏畅┅┅ㄤ彐躅潋狩箴灬箬ī⒛蜥翦扉脲箴灬箬筱蝈孱箐旌潋狩篝蜷铉箫扉洵⑷屐祜迎酗铉、ōǒ鏖钿秣鏖漪瑾博窗舶┅ㄤ彐躅潋狩骛ī⒛蜥菩麸翳筱蝈孱鏖翳骘铘嗒趑姝箜犰飙骘铘М箐旌鏖翳骘铘ㄦ趑姝箜犰飙骘铘箐旌潋狩篝蜷铉箫扉洵黩轸瀛麸篝蜷铉趄躅汜翦箐旌狯弪徵瀛骛螬┅舶卑恒镬矧箐旌珧邋瞠┅ㄤ彐礤翳镤玑礤潋狩è玑礤玑礤┅箐旌沆遽颦溟箴灬箐旌忪徙氇后躜驷沐箐旌溴驷蹯舡溟箴灬ㄣ镱è羼ㄧ犴瀛篝狒玑礤т镱濠ㄤ蜥鳝筱矧濠ㄤ蜥鳝鏖铑弪鏖铑弪┅麒孱ㄥㄧ犴瀛篝狒玑礤篝狎舂ㄤ蜥鳝箴灬箬┅ㄤ蜥鳝筱矧濠ㄤ蜥鳝骛螬磲疸狎＇蝈钿弪玑礤镡赍泗螵ㄡ钿沲蝌孱舡盹溟骈弪ㄤ蜥盹溟骈弪箴蜷翦沲蝌孱舡盹溟骈弪壶ǒ鏖钿秣鏖漪瑾博葫ǒ鏖钿秣桢殓梏博┅┅ㄤ彐躅盹溟骈弪豉疱盹溟骈弪ㄣ狎盹溟骈弪┅ㄤ彐躅盹溟骈弪箴蜷翦盹溟骈弪ㄣ潋盹溟骈弪┅ㄤ彐躅镳痫铄铘痨狴弪ㄣ狎蝈盹鲥痨狴弪痨狴弪螵┅ㄤ彐礤翳镤痱镢弩蟓弼孱è玑礤玑礤┅磲疸狎＇灬礅溽皓ㄡ沣屐弪狒铋飑痨狴弪螵戾è漪箐旌漪┅换领祜盹鲩铉翳疳滗戾轭犷篝狒瀹磲疸狎＇灬礅溽痨狴弪躔溽翦痨狴弪漪┅痨狴弪螵ㄣ镱换渝蝣瀹è羼ㄧ犴瀛篝狒玑礤箦蝣濠眭祠轲戾鲠祯瀛忾钿ㄤ澌箦蝣箦蝣轭绛痨狴弪箦翩鲥飙忉祆澍箦翩鲥飙忉祆澌┅ㄡ骠弪镳痫铄铘栝镳痫铄铘箦蝣轭绛痨狴弪忉祆箦翩ㄧ犴瀛篝狒玑礤ю灬┅换震溽翦忉祆鲥祜汩豉泔祆殇犷筱矧瀹è羼ㄧ犴瀛篝狒玑礤ю灬ㄣ镱蔑祆殇鏖翳盹溟骈弪矧鏖翳翳疳滗戾螽è犷沲蝌孱舡盹溟骈弪ㄣ镬扉溴忉祆盹溟骈弪箴蜷翦沲蝌孱舡盹溟骈弪┅ㄡ泗轹狒磲脲轭篝犷沐盹溟骈弪豉疱沲蝌孱舡盹溟骈弪红殒卑ㄩ鲥飙忉祆癌痨狴弪戾骠痨狴弪蜷玷舄┅箐飙黹弪吼灬筢眇戾ㄧ弭箫躅潴吼秣弪躔┅箦翩沲蝌孱舡盹溟骈弪铋飑箦翩盹洵蜥翦狃疱狎孱沐蜥钿镯徕卑┅è冀痫蟓忉祆癌箐飙黹弪吼灬筢眇戾ㄧ弭箫躅潴瑚犰飙栝舂箦翩痫蟓忉祆癌箦翩鲥飙忉祆ō鲥飙忉祆┅┅è窘痫蟓忉祆ō鏖钿秣桢殓梏ㄨ彘玷忉祆┅箐飙黹弪吼灬筢眇戾ㄧ弭箫躅潴瑚犰飙栝舂箦翩痫蟓忉祆ō鏖钿秣桢殓梏ㄨ彘玷忉祆┅箦翩鲥飙忉祆ō鲥飙忉祆┅┅戾è栝趑弪ㄣ镱è犷鲥飙忉祆癌ㄣ镬扉溴忉祆痨狴弪戾骠┅痨狴弪戾骠è犷鲥飙忉祆癌ㄣ镬扉溴忉祆痨狴弪蜷玷舄┅痨狴弪蜷玷舄┅┅麒孱栝趑弪ㄨ轸栝趑弪ㄡ骠弪镳痫铄铘栝镳痫铄铘栝趑弪忉祆矧沲蝌孱舡盹溟骈弪ㄡ钿ㄥ耢盹ㄡ痧禊＇磲疸狎＇栝趔痨狴弪螵┅盹洵蜥翦狃疱狎孱沐┅箦翩沲蝌孱舡盹溟骈弪铘蜥钿镯戾铉翳盹溟骈弪螵┅盹溟骈弪螵┅┅┅┅戾è筱矧弪ㄣ镱è痫蟓忉祆癌痨狴弪蜷玷舄è痫蟓忉祆鏖钿秣鏖漪瑾痨狴弪戾骠┅┅麒孱筱矧弪筱矧筱矧弪磲疸狎＇灬礅溽皓麒孱盹溟骈弪皓ㄥ痖蝈盹溟骈弪皓皓┅痨狴弪螵箦翩盹洵蜥翦狃疱狎孱沐蜥钿镯徕卑┅麒孱ㄥ筱矧弪痨狴弪戾骠篝镳痨狴弪蜷玷舄┅ㄩ黠瞽筱矧弪筱矧瀛镡赍泗轹濯痱镧箦翩ㄧ犴瀛篝狒玑礤т镱濠箦翩鏖铑弪筱矧弪┅痱镧箦翩ㄧ犴瀛篝狒玑礤箦蝣濠蝈箦忉祆┅┅躔溽翦忉祆漪┅┅ㄧ犴瀛潋狩玑礤箐旌躔溽翦溟箴灬┅ㄤ彐礤翳镤溴篝蝻è玑礤玑礤┅箐飙黹弪恒祜箦狨溟铹箐飙黹弪厚蹰舡黹弪┅ㄤ彐躅箴镱ī⑿灬迎酗铉戾è箴镱磲脲轭篝犷沐х犴虹犴瀛钺礤⒂酗铉┅躅鏖钿痱雉邈ㄩ铋箴镱绌ㄤ弩趄稆箴镱绌┅换筲屮艉筢鲥扉箴犷洵溟Ⅲ痫铉呼镳戾鲥＇箴镱哄邈豸徕戾舂